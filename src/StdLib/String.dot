
using Builtin.Primitive.{ int8, int32, uword }
using Extern.C.{ malloc, free }
using Char.ASCII

class String {
    static let DEFAULT_CAPACITY: UInt = 8

    var length: Int { get }
    var capacity: UInt { get }
    var charArray: int8* { get }

    init {
        length = 0
        capacity = 8
        charArray = malloc(capacity)
    }

    init (str: int8*, len: Int) {
        charArray = str
        length = len
        capacity = UInt(len as uword)
    }

    @_builtin(StringInit)
    init (str: int8*, len: uword) {
        charArray = str
        length = Int(len)
        capacity = UInt(len)
    }

    init (len: Int) {
        charArray = malloc(len + 1)
        charArray[len] = '\0'
        length = len
        capacity = UInt(len as uword)
    }

    // must be null terminated
    init (cstring: int8*) {
        let i = 0
        while cstring[i] != '\0' {
            ++i
        }

        length = i
        capacity = UInt(i as uword)
        charArray = malloc(i)
        __builtin_memcpy(charArray, cstring, i, alignof(int8))
    }

    init (c: Char) {
        capacity = DEFAULT_CAPACITY
        charArray = malloc(capacity)
        charArray[0] = c.val
        charArray[capacity - 1] = '\0'
        length = 1
    }

    init (s: String) {
        length = s.length
        capacity = s.capacity
        charArray = malloc(s.capacity + 1)
        __builtin_memcpy(charArray, s.charArray, capacity, alignof(int8))
    }

    init (chr: Char, repeat: Int) {
        charArray = malloc(repeat + 1)
        for var i = 0; i < repeat; ++i {
            charArray[i] = chr.val
        }

        charArray[repeat] = '\0'
        length = repeat
        capacity = UInt(repeat as uword)
    }

    deinit {
        // free(charArray)
    }

    private def resize {
        let newStr = malloc(capacity + 1)
        __builtin_memcpy(newStr, charArray, length + 1, alignof(int8))

        free(charArray)
        charArray = newStr
    }

    def postfix [] (index: Int) -> ref int8 {
        if index >= length {
            throw OutOfBoundsException()
        }
        
        return charArray[index]
    }

    def infix + (s: String) -> String {
        var copy = String(self)
        copy += s

        return copy
    }

    def infix += (s: String) {
        let newLen = length + s.length
        if newLen > capacity {
            while newLen > capacity {
                capacity *= 2
            }

            resize()
        }

        __builtin_memcpy(&charArray[length], s.charArray, s.length, alignof(int8))
        length = newLen
    }

    @_builtin(StringConcat)
    def infix + (p: StringRepresentable) -> String {
        return self + (p as String)
    }

    @_builtin(StringConcatEquals)
    def mutating infix += (p: StringRepresentable) {
        self += (p as String)
    }

    def append (c: int8) {
        if length == capacity {
            capacity *= 2
            resize()
        }

        ++length
        charArray[length - 1] = c
        charArray[length] = '\0'
    }

    def substr(begin: Int, len: Int = length) -> String {
        var copyLen = len
        if begin + len > length {
            copyLen = length - begin
        }

        let newStr = malloc(len)
        __builtin_memcpy(newStr, &charArray[begin], copyLen, alignof(int8))

        return String(newStr, len)
    }
}

class StringIterator with Iterator<Char> {
    let str: String
    var curr: Int

    init (str: String) {
        self.str = str
        self.curr = 0
    }

    def next -> Char? {
        if curr >= str.length {
            return none
        }
        
        return .Some(Char(str[curr++]))
    }
}

extend String with Iterable<Char> {
    def getIterator -> StringIterator {
        return StringIterator(self)
    }
}

extend String with StringRepresentable {
    def infix as String {
        return String(self)
    }
}

extend String with Hashable {
    /// djb2 hash - see http://www.cse.yorku.ca/~oz/hash.html
    def hashCode -> UInt64 {
        var hash: UInt64 = 5381
        for var i = 0; i < length; ++i {
            hash = ((hash << 5) + hash) + charArray[i]
        }

        return hash
    }
}

extend String with Equatable {
    def infix == (s: String) -> Bool {
        if length != s.length {
            return false
        }

        for let i = 0; i < length; ++i {
            if charArray[i] != s.charArray[i] {
                return false
            }
        }

        return true
    }
}

extend String with Comparable {
    def infix > (s: String) -> Bool {
        let maxLen = length > s.length ? length : s.length
        for let i = 0; i < maxLen; ++i {
            if length <= i {
                return false
            }
            if s.length <= i {
                return true
            }
            if self[i] == s[i] {
                continue
            }
            
            return Bool(self[i] > s[i])
        }

        return false
    }

    def infix < (s: String) -> Bool {
        return !(self > s) && self != s
    }

    def infix >= (s: String) -> Bool {
        return self == s || self > s
    }

    def infix <= (s: String) -> Bool {
        return self == s || self < s
    }
}

extend String with Randomizable {
    enum RandomStringKind {
        case AlphaNumeric
    }

    static def random (length: Int, kind: RandomStringKind) -> String {
        let str = String(length)
        match kind {
            case .AlphaNumeric:
                for let i in 0..length {
                    var c: int8 = ASCII.nul
                    while c == ASCII.nul {
                        let next = 48 + (Int.random() % (122 - 48))
                        if !(next > 57 && next < 65) && !(next > 90 && next < 97) {
                            c = next.val as int8
                        }
                    }

                    str[i] = c
                }
        }

        return str
    }

    static def random -> String {
        return random(10, .AlphaNumeric)
    }
}

extend String with Throwable {
    prop description: String { 
        get {
            return self
        }
    }
}