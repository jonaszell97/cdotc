
alias ByteBuffer = [Byte]

public abstract class OutputStream {
    enum BufferKind {
        /// The stream is not currently buffered.
        case Unbuffered

        /// The stream is buffered, and the buffer is owned by this class.
        case Buffered
    }

    /// Default buffer size to use for a buffered stream.
    alias DefaultBufferSize = 1024

    /// The current buffer status.
    protected var bufferKind: BufferKind

    /// The buffer used to avoid writing to the stream too frequently.
    protected var buffer: ByteBuffer

    /// C'tor for subclasses. If buffered is true, the buffer is initialized with a default capacity.
    protected init (buffered: Bool = false) {
        bufferKind = .Unbuffered
        buffer = ByteBuffer(withInitialCapacity: 0)

        if buffered {
            setBuffered()
        }
    }

    /// D'tor.
    deinit {
        assert!(buffer.empty, "subclass should flush buffer!")
    }

    /// Returns true if the stream is currently buffered.
    public prop buffered: Bool {
        return bufferKind == .Buffered
    }

    /// Returns true if this class supports writing to a specified position.
    public def virtual supportsWriteToPosition() -> Bool { return false }

    /// Flushes the buffer to the underlying stream.
    public def flush() {
        if !buffer.empty {
            flushNonEmpty()
            buffer.removeAll()
        }
    }

    /// Flushes the buffer to the underlying stream. Asserts if the buffer is empty.
    def flushNonEmpty() {
        assert!(!buffer.empty, "'flushNonEmpty' called on empty buffer!")
        writeToStream(buffer)
        buffer.removeAll()
    }

    /// Force the stream to be buffered, using the given capacity for the buffer.
    public def setBuffered(bufferSize: Int = DefaultBufferSize) {
        bufferKind = .Buffered
        buffer.reserveCapacity(bufferSize)
    }

    /// Returns the total number of bytes written.
    public def tell() -> Int { return currentPos() + getNumBytesInBuffer() }

    /// Returns the number of bytes currently buffered.
    def getNumBytesInBuffer() -> Int { return buffer.size }

    /// Write a string representable value to the stream.
    public def write <T: StringRepresentable> (_ val: T) {
        write(bytes: [Byte](elementsOf: val.toString().ascii))
    }

    /// Write a string representable value to the stream to the specified position 
    /// if supported, asserts otherwise.
    public def writeAtPosition<T: StringRepresentable>(_ val: T, at pos: Int) {
        assert!(supportsWriteToPosition(), "pwrite is not supported by this OutStream")
        assert!(pos < tell(), "writing to out-of-bounds position")

        pwriteToStream([Byte](elementsOf: val.toString().ascii), at: pos)
    }

    /// Convenience operator for easy chaining of several output operations.
    @discardableResult
    public def infix << <T: StringRepresentable>(t: T) -> Self {
        write(t)
        return self
    }

    /// Write the byte either to the buffer or directly to the stream.
    def write(byte: Byte) {
        if buffered {
            if buffer.size + 1 > buffer.capacity {
                // Flush the buffer to make space.
                flush()
            }

            buffer.append(byte)
        }
        else {
            writeToStream([Byte](singleElement: byte))
        }
    }

    /// Write the bytes either to the buffer or directly to the stream.
    def write(bytes: [Byte]) {
        if buffered {
            if buffer.size + bytes.size > buffer.capacity {
                // unlikely case: we need to write more bytes than the capacity of our buffer,
                // in that case write as many multiples of the buffer size as possible to the
                // stream and buffer the rest
                if builtin.unlikely(bytes.size > buffer.capacity) {
                    var amount = bytes.size % buffer.capacity
                    writeToStream(bytes.prefix(amount * buffer.capacity))

                    var rest = bytes.size - amount * buffer.capacity
                    if rest > 0 {
                        write(bytes: bytes.suffix(rest))
                    }

                    return
                }

                // Flush the buffer to make space.
                flush()
            }

            buffer.append(elementsOf: bytes)
        }
        else {
            writeToStream(bytes)
        }
    }

    //===--------------------------------------------------------------------===//
    // Colors
    //===--------------------------------------------------------------------===//

    public enum Color with StringRepresentable {
        case Uncolored = 0
        case Black   = 30
        case Red     = 31
        case Green   = 32
        case Yellow  = 33
        case Blue    = 34
        case Magenta = 35
        case Cyan    = 36
        case White   = 37

        /// \inherit
        def toString() -> String {
            return "\e[${self.rawValue}m"
        }
    }

    /// \return `true` iff this stream supports colors.
    public def virtual supportsColors() -> Bool { return false }

    /// Set the color of this output stream.
    /// \param c the color to set the stream to.
    public def setColor(_ c: Color) {
        if !supportsColors() {
            return
        }

        self << c
    }

    /// Reset the color of this output stream.
    public def resetColor() {
        if !supportsColors() {
            return
        }

        self << Color.Uncolored
    }

    //===--------------------------------------------------------------------===//
    // Subclass Interface
    //===--------------------------------------------------------------------===//

    /// Returns the current position in the stream.
    protected def abstract currentPos() -> Int

    /// Writes the given bytes to the underlying stream.
    protected def abstract writeToStream(_ bytes: [Byte]) -> Void

    /// Writes the given bytes to the specified position if supported, asserts otherwise.
    protected def virtual pwriteToStream(_ bytes: [Byte], at pos: Int) {
        unreachable!("pwrite is not supported by this OutputStream")
    }
}

/// An (unbuffered) output stream that writes to a string.
public class StringOutputStream: OutputStream {
    var str: String { get }
    
    /// C'tor. The stream will write to the given string.
    public init (_ str: String = "") {
        super.init(buffered: false)
        self.str = str
    }

    def override currentPos() -> Int {
        return str.size
    }

    def override writeToStream(_ bytes: [Byte]) {
        str.append(elementsOf: bytes)
    }
}

/// An output stream that ignores all output.
public class NullOutputStream: OutputStream {
    /// C'tor. The created stream will discard all output.
    fileprivate init {
        super.init(buffered: false)
    }

    def override currentPos() -> Int { return 0 }
    def override writeToStream(_ bytes: [Byte]) {}

    def override supportsWriteToPosition() -> Bool { return true }
    def override pwriteToStream(_ bytes: [Byte], at pos: Int) {}
}

/// A singleton instance of the NullOutputStream that discards all output.
public let nulls = NullOutputStream()
