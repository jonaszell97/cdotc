
import { Atomic } from atomic

internal protocol RC {
    prop strongRefcount: UInt { get set }
    prop weakRefcount: UInt { get set }
}

extend RC {
    @inline(always)
    def mutating strongRetain() {
        strongRefcount = strongRefcount + 1
    }

    @inline(always)
    def mutating strongRelease() {
        strongRefcount = strongRefcount - 1
    }

    @inline(always)
    def mutating weakRetain() {
        weakRefcount = weakRefcount + 1
    }

    @inline(always)
    def mutating weakRelease() {
        weakRefcount = weakRefcount - 1
    }
}

struct Refcounted with RC {
    var _strongRefcount: UInt
    var _weakRefcount: UInt
    let typeInfo: UnsafePtr<TypeInfo>

    prop strongRefcount: UInt {
        get { return _strongRefcount }
        set { _strongRefcount = newVal }
    }

    prop weakRefcount: UInt {
        get { return _weakRefcount }
        set { _weakRefcount = newVal }
    }
}

@inline(always) @extern(C)
public def _cdot_Retain(_ target: UnsafeMutablePtr<Refcounted>) {
    assert!(target->strongRefcount != 0, "[1;35mretaining freed object $target![0m\n")

    target->strongRetain()

    debugPrint!(Refcounting, "object %p of type %s after retain: %llu\n",
                target, target->typeInfo->name, target->strongRefcount)
}

@inline(always) @extern(C)
public def _cdot_WeakRetain(_ target: UnsafeMutablePtr<Refcounted>) {
    assert!(target->strongRefcount != 0, "[1;35mretaining freed object $target![0m\n")

    target->weakRetain()
    debugPrint!(Refcounting, "object %p of type %s after weak retain: %llu\n",
                target, target->typeInfo->name, target->weakRefcount)
}

@inline(always) @extern(C)
public def _cdot_Release(_ target: owned UnsafeMutablePtr<Refcounted>) {
    if !target {
        return
    }

    var refc = target->strongRefcount
    assert!(refc != 0, "[1;35mtrying to release object $target with refcount of 0![0m\n")

    if refc == 1 {
        target->typeInfo->deinitialize(target.raw)
        target->strongRelease()

        debugPrint!(Refcounting, "object %p of type %s after release: %llu\n",
                    target, target->typeInfo->name, target->strongRefcount)

        if target->weakRefcount == 0 {
            target.deallocate()
        }
    }
    else {
        target->strongRelease()
        debugPrint!(Refcounting, "object %p of type %s after release: %llu\n",
                    target, target->typeInfo->name, target->strongRefcount)
    }
}

@inline(always) @extern(C)
public def _cdot_WeakRelease(_ target: owned UnsafeMutablePtr<Refcounted>) {
    if !target {
        return
    }

    assert!(target->strongRefcount != 0, "[1;35mtrying to release object $target with refcount of 0![0m\n")

    target->weakRelease()

    debugPrint!(Refcounting, "object %p of type %s after weak release: %llu\n",
               target, target->typeInfo->name, target->weakRefcount)

    if target->weakRefcount == 0u {
        target.deallocate()
    }
}

struct AtomicRefcounted with RC {
    var _strongRefcount: UInt
    var _weakRefcount: UInt
    let typeInfo: UnsafePtr<TypeInfo>

    prop strongRefcount: UInt {
        get {
            var ptr = UnsafeMutablePtr<UInt._AtomicType>(rawPointer: builtin.addressOf(_strongRefcount))
            var at =  Atomic<UInt>(ptr)

            return at.load()
        }
        set {
            var ptr = UnsafeMutablePtr<UInt._AtomicType>(rawPointer: builtin.addressOf(_strongRefcount))
            var at =  Atomic<UInt>(ptr)

            at.store(newVal)
        }
    }

    prop weakRefcount: UInt {
        get {
            var ptr = UnsafeMutablePtr<UInt._AtomicType>(rawPointer: builtin.addressOf(_weakRefcount))
            var at =  Atomic<UInt>(ptr)

            return at.load()
        }
        set {
            var ptr = UnsafeMutablePtr<UInt._AtomicType>(rawPointer: builtin.addressOf(_weakRefcount))
            var at =  Atomic<UInt>(ptr)

            at.store(newVal)
        }
    }
}

@_builtin @extern(C) @inline(always)
public def _cdot_AtomicRetain(_ target: UnsafeMutablePtr<AtomicRefcounted>) {
    assert!(target->strongRefcount != 0u, "[1;35mretaining freed object $target![0m\n")

    target->strongRetain()
    debugPrint!(Refcounting, "object %p of type %s after retain: %llu\n",
                target, target->typeInfo->name, target->strongRefcount)
}

@inline(always) @extern(C)
public def _cdot_AtomicWeakRetain(_ target: UnsafeMutablePtr<AtomicRefcounted>) {
    assert!(target->strongRefcount != 0u, "[1;35mretaining freed object $target![0m\n")

    target->weakRetain()
    debugPrint!(Refcounting, "object %p of type %s after weak retain: %llu\n",
                target, target->typeInfo->name, target->weakRefcount)
}

@_builtin @extern(C) @inline(always)
public def _cdot_AtomicRelease(_ target: owned UnsafeMutablePtr<AtomicRefcounted>) {
    if !target {
        return
    }

    var refc = target->strongRefcount
    assert!(refc != 0, "[1;35mtrying to release object $target with refcount of 0![0m\n")

    if refc == 1 {
        target->typeInfo->deinitialize(target.raw)
        target->strongRelease()

        debugPrint!(Refcounting, "object %p of type %s after release: %llu\n",
                    target, target->typeInfo->name, target->strongRefcount)

        if target->weakRefcount == 0u {
            target.deallocate()
        }
    }
    else {
        target->strongRelease()
        debugPrint!(Refcounting, "object %p of type %s after release: %llu\n",
                    target, target->typeInfo->name, target->strongRefcount)
    }
}

@inline(always) @extern(C)
public def _cdot_AtomicWeakRelease(_ target: owned UnsafeMutablePtr<AtomicRefcounted>) {
    if !target {
        return
    }

    assert!(target->strongRefcount != 0u,
            "[1;35mtrying to release object $target with refcount of 0![0m\n")
    
    target->weakRelease()
    
    debugPrint!(Refcounting, "object %p of type %s after weak release: %llu\n",
                target, target->typeInfo->name, target->weakRefcount)

    if target->weakRefcount == 0u {
        target.deallocate()
    }
}

struct Box with RC {
    var _strongRefcount: UInt
    var _weakRefcount: UInt
    var deinitialize: @thin (UnsafeMutableRawPtr) -> Void
    var data: UnsafeMutableRawPtr

    prop strongRefcount: UInt {
        get { return _strongRefcount }
        set { _strongRefcount = newVal }
    }

    prop weakRefcount: UInt {
        get { return _weakRefcount }
        set { _weakRefcount = newVal }
    }
}

@extern(C)
public def _cdot_RetainBox(_ box: UnsafeMutablePtr<Box>) {
    box->strongRetain()
    debugPrint!(BoxRefcounting, "box %p after retain: %llu\n", box, box->strongRefcount)
}

@extern(C)
public def _cdot_ReleaseBox(_ box: UnsafeMutablePtr<Box>) {
    if !box {
        return
    }

    box->strongRelease()
    debugPrint!(BoxRefcounting, "box %p after release: %llu\n", box, box->strongRefcount)

    if box->strongRefcount == 0 {
        debugPrint!(BoxRefcounting, "freeing box\n")
        if builtin.icmp_ne_u64(box->deinitialize as! builtin.u64, 0) {
            box->deinitialize(box->data)
        }

        box.deallocate()
    }
}

struct AtomicBox with RC {
    var _strongRefcount: UInt
    var _weakRefcount: UInt
    var deinitialize: @thin (UnsafeMutableRawPtr) -> Void
    var data: Byte

    prop strongRefcount: UInt {
        get {
            var ptr = UnsafeMutablePtr<UInt._AtomicType>(rawPointer: builtin.addressOf(_strongRefcount))
            var at =  Atomic<UInt>(ptr)

            return at.load()
        }
        set {
            var ptr = UnsafeMutablePtr<UInt._AtomicType>(rawPointer: builtin.addressOf(_strongRefcount))
            var at =  Atomic<UInt>(ptr)

            at.store(newVal)
        }
    }

    prop weakRefcount: UInt {
        get {
            var ptr = UnsafeMutablePtr<UInt._AtomicType>(rawPointer: builtin.addressOf(_weakRefcount))
            var at =  Atomic<UInt>(ptr)

            return at.load()
        }
        set {
            var ptr = UnsafeMutablePtr<UInt._AtomicType>(rawPointer: builtin.addressOf(_weakRefcount))
            var at =  Atomic<UInt>(ptr)

            at.store(newVal)
        }
    }
}

@extern(C)
public def _cdot_AtomicRetainBox(_ box: UnsafeMutablePtr<AtomicBox>) {
    box->strongRetain()
    debugPrint!(BoxRefcounting, "box %p after retain: %llu\n", box.rawPointer, box->strongRefcount)
}

@extern(C)
public def _cdot_AtomicReleaseBox(_ box: UnsafeMutablePtr<AtomicBox>) {
    if !box {
        return
    }

    box->strongRelease()
    debugPrint!(BoxRefcounting, "box %p after release: %llu\n", box.rawPointer, box->strongRefcount)

    if box->strongRefcount == 0 {
        debugPrint!(BoxRefcounting, "freeing box\n")
        if builtin.icmp_ne_u64(box->deinitialize as! builtin.u64, 0) {
            var dataPtr: UnsafeMutableRawPtr = UnsafeMutableRawPtr(rawPointer:
                builtin.addressOf(box->data) as! builtin.RawPointer)

            box->deinitialize(dataPtr)
        }
        
        box.deallocate()
    }
}

struct Lambda with RC {
    let fn: UnsafeMutableRawPtr
    var _strongRefcount: UInt
    var captures: UnsafeMutablePtr<Box>

    prop strongRefcount: UInt {
        get { return _strongRefcount }
        set { _strongRefcount = newVal }
    }

    prop weakRefcount: UInt {
        get { unimplemented!() }
        set { unimplemented!() }
    }
}

@extern(C)
public def _cdot_RetainLambda(_ lambda: UnsafeMutablePtr<Lambda>) {
    lambda->strongRetain()
    debugPrint!(LambdaRefcounting, "lambda %p after retain: %llu\n", lambda, lambda->strongRefcount)
}

@extern(C)
public def _cdot_ReleaseLambda(_ lambda: UnsafeMutablePtr<Lambda>) {
    if !lambda {
        return
    }

    lambda->strongRelease()
    debugPrint!(LambdaRefcounting, "lambda %p after release: %llu\n", lambda, lambda->strongRefcount)

    if lambda->strongRefcount == 0 {
        _cdot_CleanupLambda(lambda)
    }
}

@extern(C)
public def _cdot_CleanupLambda(_ lambda: UnsafeMutablePtr<Lambda>) {
    debugPrint!(LambdaRefcounting, "freeing lambda\n")

    if lambda->captures {
        var i = 0
        var ptr: UnsafeMutablePtr<UnsafeMutablePtr<Box>> = UnsafeMutablePtr<UnsafeMutablePtr<Box>>(
                rawPointer: builtin.addressOf(&lambda->captures) as! builtin.RawPointer)

        loop {
            var capt = *ptr
            if !capt {
                break
            }

            _cdot_ReleaseBox(capt)
            ptr += 1
        }
    }

    lambda.deallocate()
}

struct AtomicLambda with RC {
    let fn: UnsafeMutableRawPtr
    var _strongRefcount: UInt
    var captures: UnsafeMutablePtr<AtomicBox>

    prop strongRefcount: UInt {
        get {
            var ptr = UnsafeMutablePtr<UInt._AtomicType>(rawPointer: builtin.addressOf(_strongRefcount))
            var at =  Atomic<UInt>(ptr)

            return at.load()
        }
        set {
            var ptr = UnsafeMutablePtr<UInt._AtomicType>(rawPointer: builtin.addressOf(_strongRefcount))
            var at =  Atomic<UInt>(ptr)

            at.store(newVal)
        }
    }

    prop weakRefcount: UInt {
        get { unimplemented!() }
        set { unimplemented!() }
    }
}

@extern(C)
public def _cdot_AtomicRetainLambda(_ lambda: UnsafeMutablePtr<AtomicLambda>) {
    lambda->strongRetain()
    debugPrint!(LambdaRefcounting, "lambda %p after retain: %llu\n", lambda, lambda->strongRefcount)
}

@extern(C)
public def _cdot_AtomicReleaseLambda(_ lambda: UnsafeMutablePtr<AtomicLambda>) {
    if !lambda {
        return
    }

    lambda->strongRelease()
    debugPrint!(LambdaRefcounting, "lambda %p after release: %llu\n", lambda, lambda->strongRefcount)

    if lambda->strongRefcount == 0u {
        _cdot_AtomicCleanupLambda(lambda)
    }
}

@extern(C)
public def _cdot_AtomicCleanupLambda(_ lambda: UnsafeMutablePtr<AtomicLambda>) {
    debugPrint!(LambdaRefcounting, "freeing lambda\n")

    if lambda->captures {
        var i = 0
        var ptr: UnsafeMutablePtr<UnsafeMutablePtr<AtomicBox>> = UnsafeMutablePtr<UnsafeMutablePtr<AtomicBox>>(
                rawPointer: builtin.addressOf(&lambda->captures) as! builtin.RawPointer)

        loop {
            var capt = *ptr
            if !capt {
                break
            }

            _cdot_AtomicReleaseBox(capt)
            ptr += 1
        }
    }

    lambda.deallocate()
}
