
import { RawPointer, Void } from builtin
import mem
import sys

public struct ValueWitnessTable with MoveOnly {
    /// Function that copies an existential value.
    let copy: @thin (RawPointer, RawPointer) -> Void

    /// Function that deinitializes an existential value.
    /// For classes, this is a reference to _cdot_Release, not the actual deinitializer.
    let deinitialize: @thin (RawPointer) -> Void
}

public struct ProtocolConformance {
    /// Pointer to the protocol type-info object.
    var typeInfo: UnsafePtr<TypeInfo>

    /// Pointer to the vtable for this protocol.
    var vtable: UnsafePtr<UnsafeRawPtr>

    /// Implicit conversion to bool.
    @implicit def infix as() -> Bool {
        return !typeInfo.isNull
    }
}

public struct TypeInfo {
    /// Pointer to the base class type-info object, or `null` if there is none.
    let baseClass: UnsafePtr<TypeInfo>

    /// Pointer to the vtable in case of a class or protocol.
    let vtable: UnsafePtr<UnsafeRawPtr>

    /// Pointer to the deinitializer of this type.
    let deinitialize: @thin (RawPointer) -> Void

    /// NUL-terminated cstring containing the type name.
    let name: UnsafePtr<Byte>

    /// Pointer to the value witness table of this type.
    let valueWitnessTable: UnsafePtr<ValueWitnessTable>

    /// null-terminated array of protocol conformance objects.
    let conformances: UnsafePtr<ProtocolConformance>

    /// The size of a value of this type, in bytes.
    let size: Int

    /// The alignment of a value of this type, in bytes.
    let alignment: Int

    /// The stride of a value of this type, in bytes.
    let stride: Int

    /// \return the vtable for a particular protocol.
    def getConformance(_ proto: UnsafePtr<TypeInfo>) -> UnsafePtr<ProtocolConformance> {
        if !conformances {
            return .null
        }

        var i = 0
        var conformances = conformances.copy()
        var conf = conformances + i

        while conf->typeInfo {
            i += 1

            if conf->typeInfo == proto {
                return conf
            }

            conf = conformances + i
        }

        return .null
    }
}

public struct ClassHeader {
    /// Strong reference count of this class.
    var strongRefcount: UInt

    /// Weak reference count of this class.
    var weakRefcount: UInt

    /// Pointer to the type-info object.
    let typeInfo: UnsafePtr<TypeInfo>
}

/// \return pointer to the type-info object of a class.
@extern(C) public def _cdot_GetTypeInfo(_ ptr: UnsafePtr<ClassHeader>) -> UnsafePtr<TypeInfo> {
    return ptr->typeInfo.copy()
}

@extern(C) public def _cdot_GetConformance(_ typeInfo: UnsafePtr<TypeInfo>,
                                           _ protocolTypeInfo: UnsafePtr<TypeInfo>)
                                                -> UnsafePtr<ProtocolConformance> {
    return typeInfo->getConformance(protocolTypeInfo)
}

@extern(C) public def _cdot_GetProtocolVTable(
    _ container: UnsafePtr<ExistentialContainer>,
    _ protocolTypeInfo: UnsafePtr<TypeInfo>) -> UnsafePtr<UnsafeRawPtr>
{
    var conformance: UnsafePtr<ProtocolConformance>

    // Fast-path if this existential only stores one conformance.
    if container->conformance {
        conformance = container->conformance
    }
    // Otherwise we have to find the conformance.
    else {
        conformance = container->typeInfo->getConformance(protocolTypeInfo)
    }

    return conformance->vtable.copy()
}

/// Compare two type-info objects for compatibility.
/// \return `true` iff they're compatible.
@extern(C) public def _cdot_TypeInfoCmp(_ lhs: UnsafePtr<TypeInfo>,
                                        _ rhs: UnsafePtr<TypeInfo>) -> Bool {
    var curr = lhs.copy()
    while curr {
        if curr == rhs {
            return true
        }

        curr = curr->baseClass.copy()
    }

    var conf = lhs->getConformance(rhs)
    if conf {
        return true
    }

    return false
}

struct AnyOption<T> {
    var discriminator: Int64
    var object: T
}

/// Perform a dynamic downcast from a base class to a child.
/// \return `none` if the cast failed, `.Some(value)` otherwise.
@extern(C) public def _cdot_DynamicDownCast(_ object: UnsafePtr<ClassHeader>,
                                            _ rhs: UnsafePtr<TypeInfo>,
                                            _ result: mut ref AnyOption<UnsafeRawPtr>) {
    var curr = object->typeInfo.copy()
    while curr {
        if curr == rhs {
            var ptr = UnsafeMutablePtr<AtomicRefcounted>(bitPattern: object.rawValue)
            _cdot_AtomicRetain(ptr)

            result = AnyOption<UnsafeRawPtr>(discriminator: 1, object: UnsafeRawPtr(object))
            return
        }

        curr = curr->baseClass.copy()
    }

    result = AnyOption<UnsafeRawPtr>(discriminator: 0, object: .null)
}

/// Copy function for classes.
@_builtin @extern(C)
def _cdot_CopyClass(_ result: UnsafeMutableRawPtr, _ value: UnsafeRawPtr) {
    // Get the class reference.
    var classRef = UnsafeMutablePtr<AtomicRefcounted>(bitPattern: value.rawValue)

    // Retain the value.
    _cdot_AtomicRetain(classRef)

    // Set the new value to point to the retained class.
    var resultRef = result.reinterpretCast<UnsafeRawPtr>()
    resultRef.store(value.copy())
}

public struct ExistentialContainer {
    /// Pointer to the heap allocated memory where the value resides.
    var value: UnsafeMutableRawPtr

    /// Pointer to the type-info object of the wrapped value.
    var typeInfo: UnsafePtr<TypeInfo>

    /// Pointer to the currently active protocol conformance.
    var conformance: UnsafePtr<ProtocolConformance>

    // Default C'tor.
    init {
        self.value = .null
        self.typeInfo = .null
        self.conformance = .null
    }

    // Memberwise C'tor.
    memberwise init
}

public def _isClass(_ typeInfo: UnsafePtr<TypeInfo>) -> Bool {
    return (UnsafeRawPtr(rawPointer: typeInfo->valueWitnessTable->copy as! builtin.RawPointer) 
        == UnsafeRawPtr(rawPointer: _cdot_CopyClass as! builtin.RawPointer))
}

/// Initialize an existential container for a protocol value.
/// This function takes ownership of \param value without making a copy.
@extern(C)
public def _cdot_InitializeExistential(_ value: UnsafeRawPtr,
                                       _ valueTypeInfo: UnsafePtr<TypeInfo>, 
                                       _ protocolTypeInfo: UnsafePtr<TypeInfo>,
                                       _ result: mut ref ExistentialContainer) {
    var alloc: UnsafeMutableRawPtr
    if _isClass(valueTypeInfo) {
        _cdot_AtomicRetain(UnsafeMutablePtr<AtomicRefcounted>(bitPattern: value.rawValue))
        alloc = UnsafeMutableRawPtr(bitPattern: value.rawValue)
    }
    else {
        // Heap-allocate space for the existential value.
        alloc = mem.allocate(valueTypeInfo->stride,
                             alignTo: valueTypeInfo->alignment)

        // Do a shallow copy of the value.
        mem.copy(to: alloc, from: value, valueTypeInfo->size)
    }

    // Get the protocol conformance.
    var conformance = valueTypeInfo->getConformance(protocolTypeInfo)

    // Initialize the existential container.
    result = ExistentialContainer(value: alloc, typeInfo: valueTypeInfo.copy(), conformance)
}

/// Initialize an existential container for a protocol value.
/// This function takes ownership of \param value without making a copy.
@extern(C)
public def _cdot_InitializeExistentialPreallocated(_ value: UnsafeRawPtr,
                                                   _ valueTypeInfo: UnsafePtr<TypeInfo>, 
                                                   _ protocolTypeInfo: UnsafePtr<TypeInfo>,
                                                   _ result: mut ref ExistentialContainer) {
    // Get the protocol conformance.
    var conformance = valueTypeInfo->getConformance(protocolTypeInfo)

    // Initialize the existential container.
    result = ExistentialContainer(value: UnsafeMutableRawPtr(bitPattern: value.rawValue),
                                  typeInfo: valueTypeInfo.copy(),
                                  conformance)
}

/// Deinitialize an existential container for a protocol value.
@extern(C)
public def _cdot_DeinitializeExistential(_ container: mut ref ExistentialContainer) {
    // Call the deinitializer of the existential value.
    container.typeInfo->valueWitnessTable->deinitialize(container.value.raw)

    // Free the memory allocated for the value.
    if !_isClass(container.typeInfo) {
        container.value.deallocate()
    }
}

/// Copy an existential container for a protocol value.
@_builtin @extern(C)
public def _cdot_CopyExistential(_ container: mut ref ExistentialContainer,
                                 _ result: mut ref ExistentialContainer) {
    var alloc: UnsafeMutableRawPtr
    if _isClass(container.typeInfo) {
        _cdot_AtomicRetain(UnsafeMutablePtr<AtomicRefcounted>(rawValue: container.value.rawValue))
        alloc = UnsafeMutableRawPtr(container.value)
    }
    else {
        // Heap-allocate space for the existential value.
        alloc = mem.allocate(container.typeInfo->stride,
                             alignTo: container.typeInfo->alignment)

        // Call the witness copy function to create a copy of the value.
        container.typeInfo->valueWitnessTable->copy(alloc.raw, container.value.raw)
    }

    // Initialize the existential container with the copied value.
    result = ExistentialContainer(value: alloc,
                                  typeInfo: container.typeInfo,
                                  conformance: container.conformance)
}

@_builtin @extern(C)
public def _cdot_ExistentialCast(_ container: mut ref ExistentialContainer,
                                 _ destinationProtocolTypeInfo: UnsafePtr<TypeInfo>,
                                 _ result: mut ref ExistentialContainer) {
    // Find the conformance.
    var conformance = container.typeInfo->getConformance(destinationProtocolTypeInfo)
    if !conformance {
        sys.reportFatalError("cast from value of type ${container.typeInfo->name} to type ${destinationProtocolTypeInfo->name} failed")
    }

    // Copy the container.
    _cdot_CopyExistential(&container, &result)

    // Update the conformance.
    result.conformance = conformance
}

@_builtin @extern(C)
public def _cdot_ExistentialCastFallible(_ container: mut ref ExistentialContainer,
                                         _ destinationProtocolTypeInfo: UnsafePtr<TypeInfo>,
                                         _ result: mut ref AnyOption<ExistentialContainer>) {
    // Find the conformance.
    var conformance = container.typeInfo->getConformance(destinationProtocolTypeInfo)

    // Return none if the conformance was not found.
    if !conformance {
        result.discriminator = 0
        return
    }

    // Copy the container.
    _cdot_CopyExistential(&container, &result.object)

    // Update the conformance.
    result.object.conformance = conformance
    result.discriminator = 1
}

@_builtin @extern(C)
public def _cdot_ExistentialUnwrap(_ container: mut ref ExistentialContainer,
                                   _ destinationTypeInfo: UnsafePtr<TypeInfo>,
                                   _ result: UnsafeMutableRawPtr) {
    // Check if the two types are compatible.
    var cmp = _cdot_TypeInfoCmp(destinationTypeInfo, container.typeInfo)

    // Not compatible.
    if !cmp {
        sys.reportFatalError("unwrapping ${container.typeInfo->name} to ${destinationTypeInfo->name} failed unexpectedly")
    }

    // Call the witness copy function to create a copy of the value.
    container.typeInfo->valueWitnessTable->copy(result.raw, container.value.raw)
}

@_builtin @extern(C)
public def _cdot_ExistentialUnwrapFallible(_ container: mut ref ExistentialContainer,
                                           _ destinationTypeInfo: UnsafePtr<TypeInfo>,
                                           _ result: mut ref AnyOption<UnsafeMutableRawPtr>) {
    // Check if the two types are compatible.
    var cmp = _cdot_TypeInfoCmp(destinationTypeInfo, container.typeInfo)

    // Not compatible.
    if !cmp {
        result.discriminator = 0
        return
    }

    // Call the witness copy function to create a copy of the value.
    var ref = builtin.addressOf(&result.object)
    container.typeInfo->valueWitnessTable->copy(ref, container.value.raw)

    result.discriminator = 1
}

public enum GenericArgumentValue {
    /// A generic type argument.
    case Type(UnsafePtr<TypeInfo>)

    /// A generic constant value argument.
    case Value()

    /// A variadic argument.
    case Variadic(Bool, UnsafePtr<GenericArgumentValue>)

    /// \return true iff this variadic argument is a type argument or a variadic type argument.
    prop isType: Bool {
        match self {
        case .Type(var FIXME):
            return true
        case .Value():
            return false
        case .Variadic(let type, var FIXME):
            return type
        }
    }
}

public struct GenericArgument {
    /// The name of the generic parameter.
    let name: UnsafePtr<Byte>

    /// The parameter value, depending on the argument kind.
    let value: GenericArgumentValue
}

public struct GenericEnvironment {
    /// Pointer to the enclosing generic environment, or null if none exists.
    let enclosingEnvironment: UnsafePtr<GenericEnvironment>

    /// null-terminated array of runtime template arguments.
    let genericArguments: UnsafePtr<GenericArgument>

    /// \return a generic argument with the name \param name, or `null` if it doesn't exist.
    def getGenericArgument(withName name: UnsafePtr<Byte>) -> UnsafePtr<GenericArgument> {
        var ptr = genericArguments.copy()
        if !ptr {
            return .null
        }

        while ptr->name {
            if Int32(libc.string.strcmp(ptr->name.cstring, name.cstring)) == 0 {
                return ptr
            }

            ptr += 1
        }

        if enclosingEnvironment {
            return enclosingEnvironment->getGenericArgument(withName: name)
        }

        return .null
    }
}

@_builtin @extern(C)
public def _cdot_GetGenericArgument(_ environment: UnsafePtr<GenericEnvironment>,
                                    _ depth: Int,
                                    _ index: Int) -> UnsafePtr<GenericArgument> {
    var environment = environment.copy()
    var depth = depth
    var index = index

    while depth > 0 {
        environment = environment->enclosingEnvironment.copy()
        depth -= 1
    }

    var ptr = environment->genericArguments.copy()
    while index > 0 {
        ptr += 1
        index -= 1
    }

    return ptr
}
