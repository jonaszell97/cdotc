
import builtin
import libc

/// Implemented in cdotrt
declare C public def _cdot_GetLastModifiedTime(_ fileName: libc.cstring) -> libc.long_long

macro define_int_pow_fn{
    ($($name:tok)*) => {
        $(
            @extern(C)
            public def ${_cdot_intpow_ $name}(_ base: builtin.$name, _ exp: builtin.u64)
                    -> builtin.$name {
                var base = base
                var exp = exp
                var result: builtin.$name = 1

                while builtin.icmp_ne_u64(exp, 0) {
                    if builtin.icmp_ne_u64(builtin.and_u64(exp, 1), 0) {
                        result = builtin.${mul_ $name}(result, base)
                    }

                    exp = builtin.ashr_u64(exp, 1)
                    base = builtin.${mul_ $name}(base, base)
                }

                return result
            }
        )...
    }
}

define_int_pow_fn!{ i8 u8 i16 u16 i32 u32 i64 u64 i128 u128 }